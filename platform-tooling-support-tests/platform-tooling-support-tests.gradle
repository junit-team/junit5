plugins {
	id 'java'
}

// https://github.com/schauder/degraph/issues/87
degraph.enabled = false

// Use Java 10
project.ext.javaVersion = JavaVersion.VERSION_1_10
afterEvaluate {
	tasks.withType(JavaCompile) {
		sourceCompatibility = project.ext.javaVersion
		targetCompatibility = project.ext.javaVersion
		options.encoding = 'UTF-8'
		options.compilerArgs += '-parameters'
		options.compilerArgs += ['--release', project.ext.javaVersion.majorVersion]
	}
}

dependencies {
	implementation('commons-io:commons-io:2.6') {
		because 'downloading tool archives and moving/deleting directory trees'
	}

	testImplementation(project(':junit-jupiter-api'))
	testRuntimeOnly(project(':junit-platform-launcher'))
	testRuntimeOnly(project(':junit-jupiter-engine'))
}

test {
	// Opt-in via system property: '-Dplatform.tooling.support.tests.enabled=true'
	enabled = Boolean.getBoolean('platform.tooling.support.tests.enabled')

	// All maven-aware projects must be installed, i.e. published to the local repository
	rootProject.ext.mavenizedProjects.each { name ->
		dependsOn project(":${name}").publishToMavenLocal
	}

	useJUnitPlatform()
}
